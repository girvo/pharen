(ns pharen.phake)
(use pharen.path as path)
(define "pharen\phake\PHAKE_SYSTEM" (realpath (dirname __FILE__)))
(define "pharen\phake\PHAREN_SYSTEM" (path.join PHAKE-SYSTEM "../.."))
(define "pharen\phake\PROJECT_SYSTEM" (getcwd))

(require-once (. PHAREN-SYSTEM "/pharen.php"))
(compile-lang)

(fn proj (name attrs)
  "Do stuff with project info here")

(fn project-path ([f ""])
  (str-replace "\\" "/" (path.join PROJECT-SYSTEM f)))

(fn not-dots (dir)
  (and (!== "." dir) (!== ".." dir)))

(fn compile-with-flag (flag file)
  (set-flag flag)
  (compile-file file)
  (unset-flag flag))

(fn compile-except (excepts file [output-dir NULL])
  (when (not (in-array file (arr excepts)))
    (compile-file file)))

(fn is-phn (f)
  (let [pathinfo (pathinfo f)]
    (and (isset (:pathinfo "extension"))
         (== (:pathinfo "extension") "phn"))))

(fn compile-dir (dir [compile-func #compile-file])
  (map 
    (lambda (f)
            (def file (path.join dir f))
            (cond
              ((and (not-dots f) (is-dir file)) (compile-dir file compile-func))
              ((is-phn file) (do
                               ($compile-func file (dirname file))
                               (print (. "Compiled " file "\n"))))))
    (scandir dir 1)))


(defmacro task (name desc &body)
  (def name-str name)
  '(fn ~name () 
     (print (. "Running " ~name-str ": " ~desc "\n"))
      ~@body))

(compile-with-flag "executable" (. PHAKE-SYSTEM "/phake.phn"))
(compile-file (. PHAKE-SYSTEM "/builtins.phn"))
(require (. PHAKE-SYSTEM "/builtins.php"))

(def phakefile (project-path "/phakefile"))
(when (file-exists phakefile)
  (compile-file phakefile)
  (require (. phakefile ".php")))

(if (> (count argv) 1)
  ((:argv 1))
  (print "Doing nothing\n"))
